package ShopIntro
import GladiatorSystem
import Scene
import GameSettings
import Abilities
import TimedSpecialEffects
import ReadyHandler
import Round
import Mastery
import Attempts
import SelectionLock



public function player.startShopIntro(GladiatorType gladiatorType)
    
    let scene = new Scene()
    
    scene.do() p ->
        p.resetToGameCamera(0)
        p.setCameraPosition(SHOP_SPAWN)
        if not DEBUG or LOCK_CAM_BOUNDS
            p.setCameraBoundsToRect(gg_rct_Shop_CamBounds)

    if( not DEBUG or not SKIP_SHOP_INTRO)

        scene
        ..wait(1)
        ..do( p -> p.fadeIn(1) )
        ..wait(1)
        ..do( p -> p.createGladiator(gladiatorType))
        ..wait(1.25)
        ..do( p -> p.getGladiator().getSpawnSound().play() )
        ..do(p -> p.disallowSelectionLocking())
        ..do(p -> p.enableSelection())
        ..do(p -> p.selectGladiator())
        ..do( p -> p.enableMasteryShop() )
        ..do( p -> p.toggleAttemptsWindow(true) )
        ..wait(3)
        
        ..do() p ->
            SND_MESSAGE.playForPlayer(p)
            p.print("When you have prepared yourself write '{0}' to start the round.".format("-ready".withColor(COLOR_HIGHLIGHT)),5)
        scene
        ..wait(8)
        ..do() p ->
            Round.printNextRound(p)
            p.enableReadyCommand()
    
    else
        scene
        ..do( p -> p.fadeIn(0) )
        ..do( p -> p.createGladiator(gladiatorType))
        ..do(p -> p.disallowSelectionLocking())
        ..do(p -> p.enableSelection())
        ..do(p -> p.selectGladiator())
        ..do( p -> p.enableMasteryShop() )
        ..do( p -> p.toggleAttemptsWindow(true) )
        ..do() p ->
            Round.printNextRound(p)
            p.enableReadyCommand()
    scene.play(this)
    


function player.createGladiator(GladiatorType gladiatorType)
    
    let gladiator = this.newGladiator(gladiatorType, SHOP_SPAWN, angle(3*PIHALF))

    // Unlock All Initial Abilities
    for i=0 to UNLOCK_ABILITY_ROUNDS.length-1
        if( UNLOCK_ABILITY_ROUNDS[i] == 0 )
            gladiator.unlockNextAbility()
    
    addEffect(Abilities.massTeleportCaster, gladiator.getUnit().getPos() )
    ..setDuration(3)