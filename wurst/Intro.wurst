package Intro
import GameSettings
import StringUtilsX
import PickHero
import Scene
import Players
import Difficulty


public function player.startIntro()

    let scene = new Scene()

    if( localPlayer == this)
        ClearTextMessages()

    if( not DEBUG or not SKIP_INTRO )
        scene.wait(3)
        scene.do() p ->
            SND_MESSAGE.playForPlayer(p) 
            p.print(
                "Welcome Gladiator!".withColor(COLOR_HIGHLIGHT) + 
                "\nYou have been chosen to enter the arena, where you must face the fiercest of opponents, whose only goal is to make you regret the day you "+
                "stepped onto their turf. To be named the true champion, you must survive " + FINAL_ROUND.toString() +" merciless rounds of slaughter."
            , 12)
        scene.wait(14)

    if( not DEBUG or not SKIP_DIFFICULTY )
        scene.do() p ->
            if( p == getHost() )
                p.print("\nChoose a difficulty", 1000)
                p.showDifficultyDialog() difficulty ->
                    new Scene()
                    ..do(  -> ClearTextMessages() )
                    ..do(p -> p.print("\nChosen difficulty: " + difficulty.toString().toLowerCase().firstUpper().withColor(COLOR_HIGHLIGHT), 3))
                    ..wait(4)
                    ..do(p -> p.startPickHero())
                    ..play(allPlayers)
            else
                p.print( "\n" + getHost().getNameColored() + " is choosing a difficulty.", 6)
    
    else
        scene.do( p -> p.startPickHero() )
    scene.play(this)