package Voodoo

import Charge
import ObjectIdGenerator
import SoundUtils
import Buff
import Stats
import TimedSpecialEffects


//================================================================================================================================================================================================
// Settings

public let CURSE_ENERGY_REGEN_RED = 0.75

public let CURSE_ATTACK_SPEED_RED = 0.75

public let CURSE_DURATION = 5.

let CURSE = compiletime(ABIL_ID_GEN.next())
let CURSE_BUFF = compiletime(defineBuff("Cursed", "Reduced movement speed, attack speed and energy regeneration.", Icons.bTNDoom, Abilities.curseTarget, "overehead", ""))

let SND_CURSE = new SoundDefinition(Sounds.parasite1, false, true)
let SND_LAUGH = new SoundDefinition(Sounds.sargerasLaugh, false, true)

//================================================================================================================================================================================================


public function unit.consumeVoodoo() returns real
    let voodoo = this.getCharge()
    this.setCharge(0)
    return voodoo.toReal()

public function unit.voodooFull()
    this
    ..setCharge(0)
    ..setMana(0)

    SND_CURSE.playOnPoint(this.getPos().withTerrainZ(200)).snd.setVolume(75)
    SND_LAUGH.playOnPoint(this.getPos().withTerrainZ(200))

    addEffect(Abilities.animateDeadTarget, this.getPos().withTerrainZ())
    ..setScale(1.5)
    ..setColor(255, 0, 0)
    ..setDuration(4)

    addEffect(Abilities.avengerMissile, this.getPos().withTerrainZ(50))
    ..setScale(2.)
    ..destr()

    addEffect(Abilities.deathandDecayTarget, this.getPos().withTerrainZ(60))
    ..setScale(2.)
    ..setDuration(4.)

    this.applyBuff(new CurseBuff(), this, CURSE_DURATION)

    

class CurseBuff extends Buff

    construct()
        super(CURSE_BUFF, false)
    
    override function onApply()
        if( target != null )
            // Reducing movement speed by 100% will bring it to it's minimum value of 50
            // This value is set within the editor constants
            target.adjustStatPercentage(STAT_MOVESPEED, -1.0)
            target.adjustStatPercentage(STAT_ATTACKSPEED, -CURSE_ATTACK_SPEED_RED)
            target.adjustStatPercentage(STAT_ENERGY_REGEN, -CURSE_ENERGY_REGEN_RED)

    override function onRemove()
        if( target != null )
            target.adjustStatPercentage(STAT_MOVESPEED, 1.0)
            target.adjustStatPercentage(STAT_ATTACKSPEED, CURSE_ATTACK_SPEED_RED)
            target.adjustStatPercentage(STAT_ENERGY_REGEN, CURSE_ENERGY_REGEN_RED)